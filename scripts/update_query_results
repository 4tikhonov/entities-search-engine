#!/usr/bin/env coffee
require 'colors'
{ exec } = require 'child_process'
callOneByOne = require './lib/call_one_by_one'
wdsparql = "./node_modules/wikidata-cli/bin/wdsparql -s"
sparqlFolder = './queries/sparql'
types = require('./lib/types_parser')(sparqlFolder, 'rq')
fs = require 'fs'

filePath = (type)-> "./queries/results/#{type}.json"

archive = (type)->
  file = filePath type
  try
    { mtime } = fs.statSync file
    timestamp = mtime.toISOString().replace(':', '-').split(':')[0]
    renamed = file.replace '.json', ".#{timestamp}.json"
    return "[ -e #{file} ] && mv #{file} #{renamed}"
  catch err
    console.log err.message
    return "echo 'no file archived'"

update = (type)->
  "#{wdsparql} #{sparqlFolder}/#{type}.rq > ./queries/results/#{type}.json"

makeQuery = (type, callback)->
  cmd = "#{archive(type)} ; #{update(type)}"
  exec cmd, (err, res)->
    if err then callback err
    else callback null, res

callOneByOne types, 'query update', makeQuery
