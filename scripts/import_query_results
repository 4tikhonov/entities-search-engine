#!/usr/bin/env coffee
resultsFolder = './queries/results'
fs = require 'fs'
wdk = require 'wikidata-sdk'
_ = require '../lib/utils'

types = require('./lib/types_parser')(resultsFolder, 'json')
callOneByOne = require './lib/call_one_by_one'
fetchAndPutEntitiesFromUris = require '../lib/fetch_and_put_entities_from_uris'

importEntities = (type)->
  # Beware of the path leading dot: require works from __dirname
  # not from process.cwd()
  uris = require ".#{resultsFolder}/#{type}.json"
    # filtering-out properties and blank nodes (type: bnode)
    .filter wdk.isItemId
    .map (id)-> "wd:#{id}"

  return fetchAndPutEntitiesFromUris type, uris
  .then -> _.success type, 'done'
  .catch (err)->
    _.error [ err, type ], 'failed'
    throw err

callOneByOne types, 'import', importEntities
.then -> _.success types, 'imports done'
.catch (err)-> _.error [ err, types ], 'imports err'
